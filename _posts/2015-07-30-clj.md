---
layout: post_page
title: Unit Testing Clojure with Expectaions and Leiningen
---

<img src="http://verse.aasemoon.com/images/5/51/Clojure-Logo.png" alt="Clojure" style="width: 150px;"/><img src="http://leiningen.org/img/leiningen.jpg" alt="Leiningen" style="width: 150px;"/> 
  
### Expectations

[Expectations](http://jayfields.com/expectations/index.html) is a minimal unit testing framework and assertion library build on the idea of "one assertion per test". I prefer using it as an alternative to clojure.test for small projects.
 
{% highlight clojure %}
;; Assertions follow the format: (expect expected actual)

(expect 1 1)
(expect 3 (count [1 2 3]))
{% endhighlight %}

---
  
### Leiningen

[Leiningen](http://leiningen.org) is a project build and dependency management tool for Clojure projects which focuses on a declarative syntax for specifying configurations for projects.

Follow the instructions [here](http://leiningen.org/#install) to set it up.

Once installed, hit ```lein version``` in a terminal to verify everything works.
You should something similar to ```Leiningen 2.5.1 on Java 1.8.0_45 Java HotSpot(TM) 64-Bit Server VM``` as the output.

---

### Generate a new project

From a terminal, use ```lein new app [app-name]``` to generate a new project based on the 'app' template. Leiningen creates a folder with the specified name, creates the necessary directory structure withing it and populates it with sample code.

{% highlight bash %}
$ lein new app mathworks
Generating a project called mathworks based on the 'app' template.
$ ls
mathworks
$ tree mathworks/
mathworks/
|-- doc
|   `-- intro.md
|-- LICENSE
|-- project.clj
|-- README.md
|-- resources
|-- src
|   `-- mathworks
|       `-- core.clj
`-- test
    `-- mathworks
        `-- core_test.clj

6 directories, 6 files
{% endhighlight %}

Open ```project.clj``` and add expectaions as a dependency.

{% highlight clojure %}
(defproject mathworks "0.1.0-SNAPSHOT"
  :description "Mathworks"
  :license {:name "Eclipse Public License"
            :url "http://www.eclipse.org/legal/epl-v10.html"}
  :dependencies [[org.clojure/clojure "1.6.0"]
  		[expectations "2.0.9"]])
{% endhighlight %}

---

### Write a (failing) test

By default, leiningen will generate a test folder with a failing test based on clojure.test. Open up ```test/mathworks/core_test.clj``` and change it to use Expectations as below:

{% highlight clojure %}
(ns mathworks.core-test
  (:require [expectations :refer :all]
            [mathworks.core :refer :all]))

;; Given a set of numbers, it should return the primes from the set
;; Input: [1 2 3 4 5 6 7 8 9 10]  Output: [2 3 5 7]
(expect [2 3 5 7] (primes [1 2 3 4 5 6 7 8 9 10]))

{% endhighlight %}

---

### Add a test runner
We now need a plugin that allows us to run the tests via leiningen. I prefer using [lein-autoexpect](https://github.com/jakemcc/lein-autoexpect) because it re-runs all the tests whenever there is a change in the source code. This a blessing given the long load time Clojure has.

To add the pluging, specify it in your ```project.clj```:

{% highlight clojure %}
(defproject mathworks "0.1.0-SNAPSHOT"
  :description "Mathworks"
  :license {:name "Eclipse Public License"
            :url "http://www.eclipse.org/legal/epl-v10.html"}
  :dependencies [[org.clojure/clojure "1.6.0"]
  		[expectations "2.0.9"]]
  :plugins [[lein-autoexpect "1.6.0"]])
{% endhighlight %}

*Note: The double '[[' is required for leiningen to read the file correctly event though we only have one plugin.*

---

### Run the test
Navigate to the folder containing project.clj and run ```lein autoexpect```. (First time runs may take a while since leiningen resolves and fetches dependencies from the web)

The output looks like:

```
$ lein autoexpect

*********************************************
*************** Running tests ***************
:reloading (mathworks.core mathworks.core-test)
:error-while-loading mathworks.core-test
Error refreshing environment: java.lang.RuntimeException: Unable to resolve symbol: primes in this context, compiling:(mathworks/core_test.clj:7:19)
Tests completed at 12:37:11.321
```

Notice that the command doesn't exit immediately since autoexpect is watching for changes. 

Modify ```core.clj``` to get rid of syntax errors:

{% highlight clojure %}
(ns mathworks.core
  (:gen-class))

(defn primes [numbers] [])
{% endhighlight %}

Go back to the term with autoexpect running and you should see that the modified test has completed:

```
*********************************************
*************** Running tests ***************
:reloading (mathworks.core mathworks.core-test)

failure in (core_test.clj:7) : mathworks.core-test
(expect [2 3 5 7] (primes [1 2 3 4 5 6 7 8 9 10]))

           expected: [2 3 5 7] 
                was: []

           in expected, not actual: [2 3 5 7]
           in actual, not expected: null
           expected is larger than actual

Ran 1 tests containing 1 assertions in 39 msecs
1 failures, 0 errors.
Tests completed at 12:38:25.090
```

---


### Make the test pass

_Finally_, complete the ```primes``` function to make the test pass:

{% highlight clojure %}
(ns mathworks.core
  (:gen-class))

(defn divisible? [x y] (zero? (mod x y)))

(defn prime? [num]
  (or 
 	(= num 2) 
 	(and 
 		(not (= 1 num))
 		(not (even? num)) 
 		(not-any? #(divisible? num %) (range 3 (inc (Math/sqrt num)) 2)))))

(defn primes [numbers] (filter prime? numbers))
{% endhighlight %}

Voila

```
*********************************************
*************** Running tests ***************
:reloading (mathworks.core mathworks.core-test)

Ran 1 tests containing 1 assertions in 5 msecs
0 failures, 0 errors.
Tests completed at 12:45:22.874
```
---

![alt text](http://media.giphy.com/media/pa37AAGzKXoek/giphy.gif "Happy dance")